plugins {
	id 'java'
	id 'eclipse'
	id 'maven-publish'
	id 'distribution'

	id "com.github.ben-manes.versions" version "0.51.0"

	id 'synapticloop.copyrightr' version '1.3.1'
}


description = """Templar templating engine"""
version = findProperty("version")
group = 'com.synapticloop'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
	withSourcesJar()
	withJavadocJar()
}


test {
	include '**/*Test.class' 
	maxParallelForks = 1
	forkEvery = 50 
}

repositories {
	mavenLocal()
	mavenCentral()
	maven { url 'https://jitpack.io' }
	maven { url "https://reposilite.synapticloop.com/releases" }
}

dependencies {
	implementation 'org.json:json:20240303'

	testImplementation 'junit:junit:4.13.2'
	testImplementation 'org.mockito:mockito-core:5.11.0'
}

// we always want to fail on version conflict
configurations.all {
	resolutionStrategy {
		failOnVersionConflict()
		force 'xerces:xmlParserAPIs:2.6.2',
					'xerces:xercesImpl:2.6.2',
					'xml-apis:xml-apis:2.0.2'
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {
		maven {
			name 'synapticloop'
			url "https://reposilite.synapticloop.com/releases"
			credentials {
				username REPOSILITE_USER
				password REPOSILITE_TOKEN
			}
		}
		mavenLocal()
	}
}

copyrightr {
	dryRun = false
	excludes = [
		"src/test/java/**/*.java"
	]
}